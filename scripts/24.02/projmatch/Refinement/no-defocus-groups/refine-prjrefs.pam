 ([iter],[ang-step],[prj-radius],[qsub])

 ; <html><head><title>Creates reference projections on parallel cluster</title></head><body><pre>
 ;
 ; SOURCE: spider/docs/techs/recon1/Procs/pub-prjrefs.spi
 ;         Reference projections parallelized   ArDean Leith  Jan 2016
 ;
 ; PURPOSE: Create stacks stacks of reference projections ('PJ 3F') from both subset reference volumes
 ;          in parallel. Waits for all subscribing nodes to finish their projections.

 ; I/O Registers & files are set in: <a href="refine-settings.spi">refine-settings.spi</a>
 ;
 ; INPUT REGISTERS:
 ;   [iter]               Current iteration
 ;   [ang-step]     Current angular step
 ;   [prj-radius]         Projection radius
 ;   [qsub]               Queing system (PBS vs PubSub)
 ;
 ; INPUT FILES: (##' denotes iteration,  '%' denotes subset, and '***' denotes group)
 ;   [iter_refangs]       work/ref_angs_##            Reference angles doc file       (one/iter)
 ;   [vol_s]              final/vol_##_s%             Current subset reference volume (two/iter)
 ;   [vol_orig]         ../vol01            Initial reference volume         (one)
 ;
 ; OUTPUT FILES:
 ;   [ref_projs_s]        work/ref_projs_##_s%        Reference projections stacks (two/iter)
 ;
 ; -------------------------------- END BATCH HEADER ----------------------------

 ; Create stacks holding projections from both current reference volumes

 UD N [num-angs]                ; Get number of reference images used
   [iter_refangs]               ; Reference images angles doc. file    (input)

 IF ( [iter] == 1 ) THEN
   ; Project initial single reference volume

   SYS
     echo  " Iteration: {%I0%[iter]}  Projecting initial reference volume: [vol_orig]   Step: {%I%[ang-step]} deg.   Projections: {%I0%[num-angs]}"

   PJ 3F                      ; Projection operation
     [vol_orig]                  ; Current reference volume             (input)
     [prj-radius]             ; Radius of object
     1-[num-angs]             ; Reference angles used
     [iter_refangs]           ; Reference angles doc file            (input)
     [ref_projs]@******     ; Reference projection stack template  (output)

 ELSE

   SYS
     echo  " Iteration: {%I0%[iter]}  Projecting: [vol]_s1 and [vol]_s2   Step: {%I%[ang-step]} deg.   Projections: {%I0%[num-angs]}"

   DO [s] = 1,2                 ; Loop over subvolumes
     PJ 3F                      ; Projection operation
       [vol_s]                  ; Current reference volume             (input)
       [prj-radius]             ; Radius of object
       1-[num-angs]             ; Reference angles used
       [iter_refangs]           ; Reference angles doc file            (input)
       [ref_projs_s]@******     ; Reference projection stack template  (output)
   ENDDO                        ; End of: Loop over subvolumes

 ENDIF

 RE
 ; </pre></body></html>
